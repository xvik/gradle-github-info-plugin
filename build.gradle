plugins {
    id 'groovy'
    id 'jacoco'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'net.researchgate.release' version '2.6.0'
    id 'ru.vyarus.quality' version '3.0.0'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'ru.vyarus.java-lib' version '1.0.5'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.3"
}

sourceCompatibility = 1.7

wrapper {
    gradleVersion = '4.6'
}

repositories { jcenter(); maven { url "https://plugins.gradle.org/m2/"} }
dependencies {
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    testCompile 'ru.vyarus:gradle-java-lib-plugin:1.0.5'
    testCompile 'com.gradle.publish:plugin-publish-plugin:0.9.10'
}

group = 'ru.vyarus'
description = 'Gradle Github info plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['gradle', 'github']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

pluginBundle {
    description = 'Generates common Github links by convention and configures common plugins'
    tags = ['github']

    plugins {
        githubInfoPlugin {
            id = 'ru.vyarus.github-info'
            displayName = project.description
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload, publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates {
    doFirst {
        project.group = 'foo'
    }
    revision = 'release'
}
jacocoTestReport.reports.xml.enabled = true
generateJacocoTestKitProperties.destinationFile = file('build/jacoco/test.exec').absolutePath.replace("\\", '/')